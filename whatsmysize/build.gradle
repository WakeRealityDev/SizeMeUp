apply plugin: 'com.android.application'

// externally saved (not in git source control) signing key
// gradle.properties needs to have defined line:
//   WakeReality_androidB.signing=/home/adminsag/app_signing/WakeReality_androidB
if (project.hasProperty("WakeReality_androidB.signing")
        && new File(project.property("WakeReality_androidB.signing") + ".gradle").exists()) {
    apply from: project.property("WakeReality_androidB.signing") + ".gradle";
}


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.wakereality.sysutil.sizemeup"
        minSdkVersion 9
        targetSdkVersion 25

        versionCode 7
        versionName "1.1.7"

        signingConfig signingConfigs.WakeReality_androidB
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        experimental {
            applicationIdSuffix ".experimental"
            versionNameSuffix "-Experimental"
            minSdkVersion 9
            signingConfig signingConfigs.WakeReality_androidB
        }
        standard {
            minSdkVersion 9
            signingConfig signingConfigs.WakeReality_androidB
        }
    }
}

if (ext.has('keyStorePassword')) {
    android.signingConfigs {
        release {
            keyAlias "sizemeup"
        }
    }
    android.signingConfigs.release.storeFile = new File(ext.keyStoreFile)
    android.signingConfigs.release.storePassword = ext.keyStorePassword;
    android.signingConfigs.release.keyPassword = ext.keyAliasPassword;
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}

dependencies {
    compile 'com.android.support:support-v4:25.2.0'
}
